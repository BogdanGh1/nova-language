Function -> function function_id ( Id_List ) { Instructions }
Id_List -> id InitialValue Id_List1 | #
Id_List1 -> , id InitialValue Id_List1 | #
InitialValue -> = Expression | #
Instructions -> Instruction Instructions | #
Instruction -> DefineInstruction | DefineGlobalInstruction | AttributionInstruction | FunctionCallInstruction | ReturnInstruction | IfInstruction | WhileInstruction | ForInstruction | DefineArrayInstruction | DefineGlobalArrayInstruction
DefineInstruction -> let Id_List ;
DefineGlobalInstruction -> global let Id_List ;
DefineArrayInstruction -> array_let ArrayIndexes Id_List ;
DefineGlobalArrayInstruction -> array_global array_let ArrayIndexes Id_List ;
AttributionInstruction -> id = Expression ; | ArrayRef = Expression ;
FunctionCallInstruction -> FunctionCall ;
ReturnInstruction -> return ReturnValue ;
IfInstruction -> if ( Condition ) { Instructions } Else
Else -> else { Instructions } | #
WhileInstruction -> while ( Condition ) { Instructions }
ForInstruction -> for ( DefineInstruction Condition ; Increment ) { Instructions }
Increment -> id = Expression | ArrayRef = Expression
Condition -> AndTerm C1
C1 -> and AndTerm C1 | #
AndTerm -> OrTerm AndTerm1
AndTerm1 -> or OrTerm AndTerm1 | #
OrTerm -> Expression RelValue
RelValue -> == Expression | != Expression | <= Expression | >= Expression | < Expression | > Expression
ReturnValue -> Expression | #
Expression -> T E1 | - Expression
E1 -> + T E1 | - T E1 | #
T -> F T1
T1 -> * F T1 | / F T1 | #
F -> ( Expression ) | id | const | FunctionCall | ArrayValue
ArrayValue -> array_id ArrayIndexes
ArrayRef -> array_id ArrayIndexes
ArrayIndexes -> [ Expression ] ArrayIndexes | #
FunctionCall -> function_id ( Parameters )
Parameters -> Expression Parameters1 | #
Parameters1 -> , Expression Parameters1 | #